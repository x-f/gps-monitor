set terminal png size 1200,500

set datafile separator ","

set xdata time
set xlabel "Time (UTC)"
set timefmt '%Y-%m-%d %H:%M:%S'
set format x "%H:%M"

set xrange ["{{LOG_TS_START}}":"{{LOG_TS_END}}"]

# set ylabel "diff"
set grid
set key off
set pointsize 0.5

set title font "sans, 18"


set title "GPS satellites in use{{PLOT_TITLE}}"
set output "{{PLOT_PATH}}/satellites.png"
set yrange [0:12]
plot '{{LOG_FILE}}' using 1:5 with lines lc rgb "#cccccc", \
  '' using 1:5 with lines linewidth 2 lt 1 smooth bezier
set autoscale y

set title "GPS position deviation – altitude{{PLOT_TITLE}}"
set yrange [*<-100:100<*]
set output "{{PLOT_PATH}}/altitude-d.png"
plot '{{LOG_FILE}}' using 1:8 with lines lc 3
unset yrange


set key

set title "GPS position deviation{{PLOT_TITLE}}"
#set ytics 0.0001 nomirror 
set yrange [*<-0.001:0.001<*]
set output "{{PLOT_PATH}}/position.png"
plot \
  '{{LOG_FILE}}' using 1:6 with lines lc 2 title "latitude", \
  '' using 1:7 with lines lc 3 title "longitude"
unset yrange

set title "GPS position deviation – distance{{PLOT_TITLE}}"
set yrange [*<10:100<*]
set output "{{PLOT_PATH}}/distance.png"
plot \
  '{{LOG_FILE}}' using 1:9 with impulses lc 1 title "" #, \
  #'' using 1:9 with lines linewidth 2 lt -1 smooth bezier title ""
unset yrange


set title "GPS position deviation – combined{{PLOT_TITLE}}"
set yrange [*<10:100<*]
set y2range [*<-0.001:0.001<*]
set ytics nomirror #tc lt 1
set y2tics 0.0005 nomirror #tc lt 3
set mytics
set my2tics
set output "{{PLOT_PATH}}/position-d.png"
plot \
  '{{LOG_FILE}}' using 1:6 with lines lc 2 axes x1y2 title "latitude", \
  '' using 1:7 with lines lc 3 axes x1y2 title "longitude", \
  '' using 1:(abs($8)) with impulses lc rgb "#bbbbbb" axes x1y1 title "altitude", \
  '' using 1:9 with impulses lc 1 axes x1y1 title "distance"
#  '' using 1:(abs($8)) with impulses lc rgb "#dddddd" axes x1y1 title "altitude", \
#  '' using 1:9 with lines linewidth 1 lt 1 title "distance", \
